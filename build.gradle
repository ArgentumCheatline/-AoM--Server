//!
//! [ALL-PROJECT]
//!
allprojects {
    repositories { jcenter() }
}

//!
//! [SUB-PROJECT]
//!
subprojects {
    //!
    //! [JAVA][IDEA]
    //!
    apply plugin: 'java'
    apply plugin: 'idea'

    //!
    //! [JAVA-8-COMPATIBILITY]
    //!
    sourceCompatibility = 1.8

    //!
    //! [REPOSITORIES]
    //!
    repositories {
        maven {
            url "http://oss.sonatype.org/content/groups/public/"
        }
        mavenCentral();
    }

    //!
    //! [API-MAIN STRUCTURE]
    //!
    sourceSets {
        api
        main {
            java {
                compileClasspath += api.output
                runtimeClasspath += api.output
            }
        }
    }

    //!
    //! [PROVIDED]
    //!
    configurations {
        provided
        apiProvided
        compile.extendsFrom apiCompile, apiProvided
        runtime.extendsFrom apiRuntime
        compile.extendsFrom provided
    }

    //!
    //! [API-MAIN STRUCTURE]
    //!
    sourceSets {
        api {
            compileClasspath += configurations.apiProvided
        }
        main {
            compileClasspath += configurations.provided
        }
    }

    //!
    //! [IDEA INTEGRATION]
    //!
    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    //!
    //! [JAR]
    //!
    jar {
        from sourceSets.api.output
    }

    //!
    //! [JAR-TASK]
    //!
    task apiJar(type: Jar) {
        appendix = "API"
        from sourceSets.api.output
    }
    build.dependsOn apiJar
}